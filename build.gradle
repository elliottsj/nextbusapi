buildscript {
    repositories {
        mavenLocal()
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.0'
    }
}

apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

sourceCompatibility = 1.6

group = 'com.elliottsj'
version = '1.1.0'

repositories {
    jcenter()
}

dependencies {
    compile 'org.simpleframework:simple-xml:2.7.1'
    compile 'com.j256.ormlite:ormlite-core:4.48'
    testCompile group: 'junit', name: 'junit', version: '4.11'
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourceJar {
                classifier 'sources'
            }
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? bintrayUser : ''
    key = project.hasProperty('bintrayKey') ? bintrayKey : ''

    publications = ['mavenJava']
    pkg {
        repo = 'maven'
        name = 'nextbusapi'
    }
}
